plugins {   
    id 'java-gradle-plugin'
    id 'maven-publish'   
    id 'com.gradle.plugin-publish' version '0.21.0'     
} 

apply from: "$rootDir/gradle/versioning.gradle"

description = 'INTERLIS Repository Creator'
group = 'ch.so.agi'

sourceCompatibility = 17 
targetCompatibility = 17 

compileJava {
    options.compilerArgs.addAll(['--release', '17'])
}

repositories {
    mavenCentral()
    //maven { url "https://jars.sogeo.services/interlis" }
    maven { url "https://jars.interlis.ch" }
} 


configurations.all {
    resolutionStrategy { 
        force 'ch.interlis:ili2c-core:5.6.3'
        force 'ch.interlis:ili2c-tool:5.6.3'
    }
}


dependencies {
    implementation group: 'ch.interlis', name: 'iox-ili', version: '1.23.4' 
    implementation group: 'ch.interlis', name: 'ili2c-core', version: '5.6.3'
    implementation group: 'ch.interlis', name: 'ili2c-tool', version: '5.6.3'
    implementation group: 'ch.interlis', name: 'ilivalidator', version: '1.14.5'
    implementation group: 'commons-io', name: 'commons-io', version: '2.12.0'     
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'      
    testImplementation gradleTestKit()
    testImplementation group: 'junit', name: 'junit', version: '4.12'    
    //testImplementation group: 'commons-io', name: 'commons-io', version: '2.6'   
}

gradlePlugin {
    plugins {
       interlisRepositoryCreatorPlugin {
           id = 'ch.so.agi.interlis-repository-creator'
           implementationClass = 'ch.so.agi.InterlisRepositoryCreatorPlugin' 
       }
   }
}

pluginBundle {
    website = 'https://github.com/sogis/interlis-repository-creator' 
    vcsUrl = 'https://github.com/sogis/interlis-repository-creator.git' 
    tags = ['interlis', 'gis', 'sdi', 'geo', 'geoinformation', 'interlis', 'repository'] 
    
    plugins { 
        interlisRepositoryCreatorPlugin {  
            displayName = 'INTERLIS Repository Creator' 
            description = 'The Gradle INTERLIS Repository Creator plugin creates an ilimodel.xml file from a bunch of INTERLIS models.' 
        }
    }
}

task setupPluginUpload() {
    description = "Converts Gradle plugin publish key and secret from environment variable to system property. (Workaround)"
    doLast {
        def key = System.env.gradlePublishKey
        def secret = System.env.gradlePublishSecret
    
        if( !key || !secret)
        {
            throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
        }
    
        System.properties.setProperty("gradle.publish.key", key)
        System.properties.setProperty("gradle.publish.secret", secret)
    }
}
tasks.publishPlugins.dependsOn tasks.setupPluginUpload

test {
    testLogging {
        events 'standard_out', 'standard_error'
    }
}
